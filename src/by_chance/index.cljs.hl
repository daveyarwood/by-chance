(page "index.html"
  (:require [by-chance.audio       :as audio]
            [by-chance.music       :as music]
            [by-chance.options     :as opts]
            [by-chance.output      :as out]
            [by-chance.performance :as perf]
            [clojure.string        :as str]))

(defc current-view :options)

(defn- classes
  [class-names]
  (zipmap class-names (repeat true)))

(defelem pill
  [{:keys [view] :as attr} kids]
  (button
    (merge
      {:class (cell= (merge (classes [:inline-block :text-white :font-bold
                                      :text-xs :py-2 :px-4 :m-1 :rounded-full
                                      :focus:outline-none])
                            {:bg-blue       (= view current-view)
                             :bg-blue-light (not= view current-view)}))
       :click #(reset! current-view view)}
      attr)
    kids))

(defelem refresh
  [attr _]
  (button
    :class "inline-block hover:text-blue mr-2"
    :click #(swap! out/refresh-count inc)
    "⟳"))

(defelem play
  [_ _]
  (let [can-play? (cell= (and out/success? audio/ready?))]
    (button
      :class (cell= (merge (classes [:inline-block :mr-2])
                           {:text-grey          (not can-play?)
                            :cursor-not-allowed (not can-play?)
                            :cursor-pointer     can-play?
                            :hover:text-blue    can-play?}))
      :click #(perf/perform!)
      "▶")))

(html
  (head
    (title "by chance :: renay aumiller + dave yarwood")
    (link :href "tailwind.min.css" :rel "stylesheet" :type "text/css"))
  (body :class "bg-grey-lighter text-blue-darkest container mx-auto"
    (div :toggle (cell= (not perf/performing?))
      (div :class "m-2"
        (h1 :class "inline-block mr-4" "by chance")
        (div :class "inline-block absolute t-16"
          (span :class "italic" "an algorithmic improvisation by")
          (br)
          (span :class "font-bold italic" "renay aumiller")
          (span :class "italic" " + ")
          (span :class "font-bold italic" "dave yarwood")))
      (div :class "mb-8"
        (pill :view :options "options")
        (pill :view :output "output")
        (refresh)
        (play))
      (opts/options-view :toggle (cell= (= :options current-view)))
      (out/output-view :toggle (cell= (= :output current-view))))
    (perf/performance :toggle perf/performing?)))

