(page "index.html"
  (:require [by-chance.audio       :as audio]
            [by-chance.options     :as opts]
            [by-chance.output      :as out]
            [by-chance.performance :as perf]
            [clojure.string        :as str]))

(defc current-view :options)

(defn- classes
  [class-names]
  (zipmap class-names (repeat true)))

(defelem pill
  [{:keys [view] :as attr} kids]
  (button
    (merge
      {:class (cell= (merge (classes [:inline-block
                                      :text-xs :py-2 :px-4 :m-1 :rounded-full
                                      :focus:outline-none])
                            {:bg-blue-light (= view current-view)
                             :font-bold     (= view current-view)
                             :text-white    (= view current-view)
                             :bg-blue-dark  (not= view current-view)
                             :italic        (not= view current-view)
                             :text-grey     (not= view current-view)}))
       :click #(reset! current-view view)}
      attr)
    kids))

(defelem refresh
  [attr _]
  (button
    :class "inline-block text-blue-lighter hover:text-blue-lightest mr-2"
    :click #(swap! out/refresh-count inc)
    "⟳"))

(defelem play
  [_ _]
  (let [can-play? (cell= (and out/success? audio/ready?))]
    (button
      :class (cell= (merge (classes [:inline-block :mr-2])
                           {:text-blue-dark           (not can-play?)
                            :cursor-not-allowed       (not can-play?)
                            :text-blue-lighter        can-play?
                            :cursor-pointer           can-play?
                            :hover:text-blue-lightest can-play?}))
      :click #(perf/perform!)
      "▶")))

(html
  (head
    (title "out of the deep blue :: renay aumiller + dave yarwood")
    (link :href "tailwind.min.css" :rel "stylesheet" :type "text/css"))
  (body :class "bg-blue text-blue-lightest container mx-auto"
    (div :toggle (cell= (not perf/performing?))
      (div :class "m-2"
        (h1 :class "inline-block mr-4" "out of the deep blue")
        (div :class "inline-block absolute t-16"
          (span :class "italic" "an algorithmic improvisation by")
          (br)
          (span :class "font-bold italic" "renay aumiller")
          (span :class "italic" " + ")
          (span :class "font-bold italic" "dave yarwood")))
      (div :class "mb-8"
        (pill :view :options "options")
        (pill :view :output "output")
        (refresh)
        (play))
      (opts/options-view :toggle (cell= (= :options current-view)))
      (out/output-view :toggle (cell= (= :output current-view))))
    (perf/performance :toggle perf/performing?)))

