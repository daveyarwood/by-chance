(ns by-chance.options
  (:require [cljs.reader    :refer (read-string)]
            [clojure.string :as    str]))

(def initial-options
  {:dance {:body-parts [], :dance-qualities []}})

(defc options nil)

(def storage-item-name "by-chance-options")

(with-timeout 0
  (reset! options
          (if-let [stored-options (-> js/window
                                      .-localStorage
                                      (.getItem storage-item-name))]
            (read-string stored-options)
            initial-options)))

(do-watch
  options
  (fn [old-value new-value]
    (when (and old-value new-value)
      (-> js/window
          .-localStorage
          (.setItem storage-item-name (prn-str new-value))))))

(defn reset-options!
  []
  (reset! options initial-options))

(def categories
  [["body parts"      [:dance :body-parts]]
   ["dance qualities" [:dance :dance-qualities]]])

(defelem options-view
  [attr _]
  (div attr
       (for [[lbl path] categories]
         (div :class "mb-8"
              (input :type "text"
                     :class "mb-2 px-2 py-2 rounded-full shadow-md
                             hover:shadow-outline focus:outline-none
                             focus:shadow-outline inline-block
                             bg-blue-light text-blue-lightest"
                     :keyup #(when (= "Enter" (.-key %))
                               (let [target (.-target %)]
                                 (when-not (str/blank? (.-value target))
                                   (swap! options update-in path
                                          conj (.-value target))
                                   (set! (.-value target) "")))))
              (p :class "font-bold inline-block ml-2" lbl)
              (p :class "w-7/8"
                (for-tpl [[i option] (cell= (->> (get-in options path)
                                                 (map-indexed vector)))]
                  (span :class "mr-3 font-normal text-sm
                               hover:text-red-dark hover:font-bold
                               cursor-pointer inline-block"
                        :click (fn []
                                 (swap! options update-in path
                                        #(vec (concat (take @i %)
                                                      (drop (inc @i) %)))))

                        option)))))
       (div
         (a :class "italic no-underline
                    text-blue-lightest hover:text-red-dark"
            :href  "#"
            :click reset-options!
            "clear all"))))

